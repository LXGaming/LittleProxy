plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "java"
    id "java-library"
    id "maven-publish"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "littleproxy"
version = "1.1.3"

configurations {
    provided {
        compile.extendsFrom(provided)
    }
}

repositories {
    jcenter()
    maven {
        name = "lxgaming"
        url = "https://dl.bintray.com/lxgaming/maven"
    }
}

dependencies {
    api("com.barchart.udt:barchart-udt-bundle:2.3.0")
    api("com.google.guava:guava:28.2-jre")
    api("commons-cli:commons-cli:1.4")
    api("io.github.lxgaming:dnssec4j:0.1.7")
    api("io.netty:netty-all:4.1.44.Final")
    api("org.apache.commons:commons-lang3:3.9")
    runtime("org.apache.logging.log4j:log4j-core:2.13.0")
    runtime("org.apache.logging.log4j:log4j-slf4j-impl:2.13.0")
    testCompile("org.apache.httpcomponents:httpclient:4.5.11")
    testCompile("org.apache.commons:commons-exec:1.3")
    testCompile("org.eclipse.jetty:jetty-server:9.4.26.v20200117")
    testCompile("org.hamcrest:hamcrest-core:2.2")
    testCompile("org.hamcrest:hamcrest-library:2.2")
    testCompile("org.junit.jupiter:junit-jupiter-api:5.6.0")
    testCompile("org.mock-server:mockserver-netty:5.8.1")
    testCompile("org.mockito:mockito-core:3.2.4")
    testCompile("org.seleniumhq.selenium:selenium-java:3.141.59")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["mavenPublication"]
    pkg {
        repo = "maven"
        name = "LittleProxy"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/LXGaming/LittleProxy.git"
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload {
    onlyIf { System.getenv("BINTRAY_KEY") != null }
    onlyIf { System.getenv("BINTRAY_USER") != null }
    onlyIf { System.getenv("TRAVIS_BUILD_NUMBER") != null }
    onlyIf { System.getenv("TRAVIS_JOB_NUMBER") ==~ /\d+\.1$/ }
    onlyIf { System.getenv("TRAVIS_PULL_REQUEST") == "false" }
}

jar {
    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }
}

javadoc {
    failOnError = false
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-${archivesBaseName}")
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}